<!DOCTYPE html>
<html>
  <head>
    <title>Title</title>
    <meta http-equiv="Content-Type" content="text/html; charset=UTF-8"/>
    <script src="remark/remark.js" type="text/javascript">
        {
            "highlightStyle": "solarized_light",
            "highlightLanguage": "no-highlight"
        }
    </script>
    <style type="text/css" media="screen">
/*
SOLARIZED HEX     16/8 TERMCOL  XTERM/HEX   L*A*B      RGB         HSB
--------- ------- ---- -------  ----------- ---------- ----------- -----------
base03    #002b36  8/4 brblack  234 #1c1c1c 15 -12 -12   0  43  54 193 100  21
base02    #073642  0/4 black    235 #262626 20 -12 -12   7  54  66 192  90  26
base01    #586e75 10/7 brgreen  240 #585858 45 -07 -07  88 110 117 194  25  46
base00    #657b83 11/7 bryellow 241 #626262 50 -07 -07 101 123 131 195  23  51
base0     #839496 12/6 brblue   244 #808080 60 -06 -03 131 148 150 186  13  59
base1     #93a1a1 14/4 brcyan   245 #8a8a8a 65 -05 -02 147 161 161 180   9  63
base2     #eee8d5  7/7 white    254 #e4e4e4 92 -00  10 238 232 213  44  11  93
base3     #fdf6e3 15/7 brwhite  230 #ffffd7 97  00  10 253 246 227  44  10  99
yellow    #b58900  3/3 yellow   136 #af8700 60  10  65 181 137   0  45 100  71
orange    #cb4b16  9/3 brred    166 #d75f00 50  50  55 203  75  22  18  89  80
red       #dc322f  1/1 red      160 #d70000 50  65  45 220  50  47   1  79  86
magenta   #d33682  5/5 magenta  125 #af005f 50  65 -05 211  54 130 331  74  83
violet    #6c71c4 13/5 brmagenta 61 #5f5faf 50  15 -45 108 113 196 237  45  77
blue      #268bd2  4/4 blue      33 #0087ff 55 -10 -45  38 139 210 205  82  82
cyan      #2aa198  6/6 cyan      37 #00afaf 60 -35 -05  42 161 152 175  74  63
green     #859900  2/2 green     64 #5f8700 60 -20  65 133 153   0  68 100  60
*/

body {
    font-size:        1.3em;
}

.slide .inverse {
    color:            #839496; /* base0  */
    background-color: #002b36; /* base03 */
}

.slide {
    color:            #657b83; /* base00 */
    background-color: #fdf6e3; /* base3 */
}

.slide pre, .slide code {
    color:            #586e75; /* base01 */
    background-color: #eee8d5; /* base2 */
}

.slide .inverse pre, .slide .inverse code {
    color:            #93a1a1; /* base1 */
    background-color: #073642; /* base02 */
}

a:link, a:visited, a:hover, a:active {
    color:            #268bd2; /* blue */
}

.footnote {
    position: absolute;
    bottom: 3em;
}
.footnote.right {
    right: 3em;
}
.code {
    font-family: Monaco, monospace;
}

    </style>
  </head>
  <body>
    <textarea id="source">
name: title
layout: true
class: center, middle, inverse
---

# Building Command-Line Apps

.footnote.right[[Chicago Perl Mongers](http://chicago.pm.org)]
---
layout: false
class: center, middle

# You're working with text

---
class: center, middle
layout: true
---

# You like the command-line
efficient and friendly

---

# You don't need a GUI

---
class: center, middle

# Shell scripts aren't strong enough
&bull; poor scalability

&bull; bytestreams aren't everything

---
class: center, middle

# Perl is an elegant solution
for a more civilized age

---
class: center, middle

# TIMTOWTDI
There Is More Than One Way To Do It

---
template: title

# Core Parts

---

# Input
&bull; Arguments: `&lt;argument&gt;`

&bull; Options: `[--option] [-h] [-R3]`

&bull; Standard Input: `STDIN`

---

# Output

&bull; Standard Output: `STDOUT`

&bull; Standard Error: `STDERR`

&bull; Exit Code: (0-255)

---

# User Documentation

---

# Code Organization

---
template: title

# Basic Scripts

---
layout: false
class: middle

.center[
# Start off right
]
```perl
use strict;
use warnings;
```

---
class: middle

.center[
# Arguments
## .code[@ARGV]
]

```perl
use strict;
use warnings;
use feature qw( say );
say "Hello, $ARGV[0]!";
```

```
$ perl greet.pl Chicago
Hello, Chicago!
```

---
class: middle

.center[
# Options
## .code[Getopt::Long]
]

```perl
use feature qw( say );
use Getopt::Long;
my %opt = (
    greeting => 'Hello',
);
GetOptions( \%opt,
    'greeting:s',
);
say "$opt{greeting}, $ARGV[0]!";
```
```
$ perl greet.pl Chicago
Hello, Chicago!
$ perl greet.pl --greeting Goodbye Cleveland
Goodbye, Cleveland!
```
---
class: middle
.center[
# Option Options
]
```perl
use Getopt::Long;
my %opt;
GetOptions( \%opt,
    'boolean',      # --boolean
    'increment+',   # --increment --increment --increment
    'toggle!',      # --toggle || --no-toggle
    'required=s',   # --required "Value"
    'optional:s',   # --optional "Value" || --optional
    'multiple=s@',  # --multiple "Foo" --multiple "Bar"
    'pairs=s%',     # --pairs foo=1 --pairs bar=1
    'name|n',       # --name || -n
);
```
---
class: middle

.center[
# Standard Input
]
```perl
use feature qw( say );
while ( my $line = <STDIN> ) {
    my @emails = ( $line =~ /(\S+[@]\S+)/g );
    say for @emails;
}
```
```
$ perl find_emails.pl < /var/spool/mail/victim
flannel@selfies.com
shabby@chic.com
Wes@Anderson.com
batch@Godard.org
trust@fund.org
```

---
class: middle

.center[
# Standard Output
]
```perl
use feature qw( say );
say 'Hello, World!';
```
```
$ perl hello.pl
Hello, World!
```

---
class: middle

.center[
# Standard Error
]
```perl
warn "Look out, World!"
die "I'm going down!\n"
```
```
$ perl warn.pl
Look out, World! at warn.pl line 2.
I'm going down!
```

---
class: middle
.center[
# STDOUT vs STDERR
]
```perl
use feature qw( say );
say "Hello, World!"
warn "Look out, World!"
die "I'm going down!\n"
```
```
$ perl stdouterr.pl 1>stdout.txt 2>stderr.txt
$ cat stdout.txt
Hello, World!
$ cat stderr.txt
Look out, World! at stdouterr.pl line 4.
I'm going down!
```
---
class: middle
.center[
# Exit Status
]
```
$ perl hello.pl
Hello, World!
$ echo $?
0
$ perl warn.pl
Look out, World! at warn.pl line 2.
I'm going down!
$ echo $?
255
```
---
class: middle
.center[
# 0 is good
Everything else is bad
]
```
$ perl hello.pl && perl warn.pl
Hello, World!
Look out, World! at warn.pl line 2.
I'm going down!

$ perl warn.pl && perl hello.pl
Look out, World! at warn.pl line 2.
I'm going down!
```
---
class: middle
.center[
# Choose your exit
]
```perl
use feature qw( say );
say "Exit code $ARGV[0]";
exit $ARGV[0];
```
```
$ perl exit.pl 0
Exit code 0

$ echo $?
0

$ perl exit.pl 1
Exit code 1

$ echo $?
1
```
---
class: center middle

# User Documentation
Pod::Usage

---
class: middle

.center[
# Plain Old Documentation
]
```
=head1 NAME

greet.pl - Say hello!

=head1 SYNOPSIS

    perl greet.pl [--greeting <greeting>] <who>

=head1 ARGUMENTS

=head2 who

Who to greet. Defaults to "World"

=head1 OPTIONS

=head2 greeting

The greeting to use. Defaults to "Hello"
```
---
class: middle
.center[
# Read The Fine Manual
]
```
$ perldoc greet.pl
GREET(1)       User Contributed Perl Documentation      GREET(1)

NAME
       greet.pl - Say hello!

SYNOPSIS
           perl greet.pl [--greeting <greeting>] [<who>]

ARGUMENTS
   who
       Who to greet. Defaults to "World".

OPTIONS
   greeting
       The greeting to use. Defaults to "Hello".

perl v5.14.2              2013-05-23                  GREET(1)
```

---
class: middle
.center[
# Help the User
Give them a `-h`and
]
```perl
use Getopt::Long;
use Pod::Usage;
my %opt;
GetOptions( \%opt,
    'help|h',
);
pod2usage(0) if $opt{help};
```
```
$ perl greet.pl -h
Usage:
        perl greet.pl [--greeting <greeting>] [<who>]
        perl -h|--help

Arguments:
  who:
    Who to greet. Defaults to "World".

Options:
  greeting:
    The greeting to use. Defaults to "Hello".

  help|h:
    Display help and usage
```
---
class: middle
.center[
# Scold the User
Tell them what they did wrong
]
```perl
use Getopt::Long;
use Pod::Usage;
my %opt;
GetOptions( \%opt,
    'help|h',
);
pod2usage(0) if $opt{help};
pod2usage("ERROR: Must give someone to greet") if !@ARGV;
```
```
$ perl greet.pl
ERROR: Must give someone to greet
Usage:
        perl greet.pl [--greeting <greeting>] [<who>]
        perl -h|--help
```
---
class: middle
.center[
# SHBANG!
]
```perl
#!/usr/bin/env perl
use feature qw( say );
say "Hello, World!";
```
```
$ ./hello.pl
bash: permission denied: ./hello.pl
$ chmod +x hello.pl
$ ./hello.pl
Hello, World!
```
---
class: middle
.center[
# Basic Script Boilerplate
]
```perl
#!/usr/bin/env perl
use strict;
use warnings;
use Getopt::Long;
use Pod::Usage;
my %opt;
GetOptions( \%opt,
    'help|h',
);
pod2usage(0) if $opt{help};

### Put your code here

=head1 NAME
=head1 SYNOPSIS
=head1 ARGUMENTS
=head1 OPTIONS
=head1 EXIT CODES
=head1 LICENSE AND COPYRIGHT
```
---

template: title

# Modulinos
Scripts as Modules

---
class: center middle
# Code Organization
---
class: center middle
# Reusable Components
---
class: center middle
# Testable Scripts
---
class: middle
.center[
# .code[int main(argv)]
]
```perl
package modulino;
sub main {
    my ( $opt, $argv ) = @_;
    ### Put your code here
    return 0;
}
exit main( {}, \@ARGV );
```
```
$ perl modulino.pm
$ ./modulino.pm
```
---
class: middle
.center[
# Add the options
]
```perl
use Getopt::Long qw( GetOptionsFromArray );
sub main {
    my ( $opt, $argv ) = @_;
    ### Put your code here
    return 0;
}
my %opt;
GetOptionsFromArray( \@ARGV, \%opt,
    'help|h',
);
exit main( \%opt, \@ARGV );
```
---
class: middle
.center[
# Use as a Module
]
```perl
if ( !caller(0) ) {
    my %opt;
    GetOptionsFromArray( \@ARGV, \%opt,
        'greeting:s',
        'help|h',
    );
    exit main( \%opt, \@ARGV );
}
```
---
class: middle
.center[
# The Greeting Modulino
]
```perl
#!/usr/bin/env perl
package greet;
use strict;
use warnings;
use feature qw( say );
use Getopt::Long qw( GetOptionsFromArray );
use Pod::Usage;

sub main {
    my ( $opt, $argv ) = @_;
    pod2usage(0) if $opt->{help};
    pod2usage("ERROR: Must give someone to greet") if !@{$argv};
    $opt->{greeting} ||= "Hello";
    say "$opt->{greeting}, $argv->[0]!";
    return 0;
}

if ( !caller(0) ) {
    my %opt;
    GetOptionsFromArray( \@ARGV, \%opt,
        'greeting:s',
        'help|h',
    );
    exit main( \%opt, \@ARGV );
}

1; # <module>.pm did not return a true value
```
---
class: middle
.center[
# Run the Modulino
]
```
$ perl greet.pm --greeting "Good Morning" "St. Louis"
Good Morning, St. Louis!
$ chmod +x greet.pm
$ ./greet.pm Chicago.PM
Hello, Chicago.PM
```
---
class: middle
.center[
# Test the Modulino
]
```perl
use Test::More;
use Capture::Tiny qw( capture );
use greet;
my ( $stdout, $stderr, $exit ) = capture {
    greet::main( {}, [ "Chicago.PM" ] );
};
is $stdout, "Hello, Chicago.PM!\n", 'correct greeting';
is $exit, 0, 'exit code: success';
done_testing;
```
```
$ prove -v greet.t
greet.t ..
ok 1 - correct greeting
ok 2 - exit code: success
1..2
ok
All tests successful.
Files=1, Tests=2,  0 wallclock secs (...)
Result: PASS
```
---
class: middle center
# Add more methods
.code[Sub::Exporter]
---
class: middle center
# Inheritance
---
class: middle
.center[
# Modulino Boilerplate
]
```perl
#!/usr/bin/env perl
package modulino;
use strict;
use warnings;
use Getopt::Long qw( GetOptionsFromArray );
use Pod::Usage;

sub main {
    my ( $opt, $argv ) = @_;
    pod2usage(0) if $opt->{help};
    ### Your code here
    return 0;
}

if ( !caller(0) ) {
    my %opt;
    GetOptionsFromArray( \@ARGV, \%opt,
        ### Your options here
        'help|h',
    );
    exit main( \%opt, \@ARGV );
}

1;

=head1 NAME
=head1 SYNOPSIS
=head1 ARGUMENTS
=head1 OPTIONS
=head1 LICENSE AND COPYRIGHT
```
---
template: title

# MooseX::Runnable
Moose-based Modulino
---
class: center middle

# Existing Module
.code[My::Module]
---
class: center middle
# Runnable Modulino
.code[mx-run My::Module]
---
class: middle
.center[
# Hello.pm
]
```perl
package Runnable::Hello;
use feature qw( say );
use Moose;
with 'MooseX::Runnable';
sub run {
    my ( $self ) = @_;
    say "Hello, World!";
    return 0;
}
1;
```
```
$ mx-run -I. Runnable::Hello
Hello, World!
```
---
class: center middle
# Run From Anywhere
.code[@INC $ENV{PERL5LIB}]
---
class: middle
.center[
# Arguments
]
```perl
package Runnable::Greet;
use feature qw( say );
use Moose;
with 'MooseX::Runnable';
sub run {
    my ( $self, $who ) = @_;
    say "Hello, $who!";
    return 0;
}
1;
```
```
$ mx-run -I. Runnable::Greet Chicago.PM
Hello, Chicago.PM!
```
---
class: middle
.center[
# Options
.code[MooseX::Getopt]
]
```perl
package Runnable::Greet;
use feature qw( say );
use Moose;
with 'MooseX::Runnable', 'MooseX::Getopt';
has greeting => (
    is      => 'ro',
    isa     => 'Str',
    default => 'Hello',
);
sub run {
    my ( $self, $who ) = @_;
    say $self->greeting . ", $who!";
    return 0;
}
1;
```
```
$ mx-run -I. Runnable::Greet --greeting Bonjour Chicago.PM
Bonjour, Chicago.PM!
```
---
class: middle center
# Caution: Two in One!
Command-line and Object

---
class: middle center
# Attributes and Documentation overlap
We're pragmatists, not purists

---
template: title
# App::Cmd
Framework for large apps
---
class: middle
.center[
## A digression
]

```
$ git help
usage: git [--version] [--exec-path[=<path>]] [--html-path] [--man-path] 
           [--info-path]
           [-p|--paginate|--no-pager] [--no-replace-objects] [--bare]
           [--git-dir=<path>] [--work-tree=<path>] [--namespace=<name>]
           [-c name=value] [--help]
           <command> [<args>]

The most commonly used git commands are:
   add        Add file contents to the index
   branch     List, create, or delete branches
   checkout   Checkout a branch or paths to the working tree
   clone      Clone a repository into a new directory
   commit     Record changes to the repository
   diff       Show changes between commits, commit and working tree, etc
   init       Create an empty git repository or reinitialize an existing one
   log        Show commit logs
   merge      Join two or more development histories together
   pull       Fetch from and merge with another repository or a local branch
   push       Update remote refs along with associated objects
   rebase     Forward-port local commits to the updated upstream head
   reset      Reset current HEAD to the specified state
   rm         Remove files from the working tree and from the index
   status     Show the working tree status
   tag        Create, list, delete or verify a tag object signed with GPG

See 'git help <command>' for more information on a specific command.
```
---
class: middle center
# App::Cmd Framework
Manages Complexity
---
class: middle center
# Command Modules
Each module, a command
---
class: middle
.center[
# 1: Application Module
]
The entry point
```perl
package App::Say;
use App::Cmd::Setup -app;
1;
```
---
class: middle
.center[
# 2: Command Module
]
```perl
package App::Say::Command::hello;
use feature qw( say );
use App::Cmd::Setup -command;
sub execute {
    my ( $self, $opt, $args ) = @_;
    say "Hello, World!";
    return 0;
}
1;
```
---
class: middle
.center[
# 3: The Runner Script
]
```perl
#!/usr/bin/env perl
use App::Say;
App::Say->run;
```
```
$ perl -I. say.pl
Hello, World!
```
---
class: middle
.center[
# Arguments
]
```perl
package App::Say::Command::greet;
use feature qw( say );
use App::Cmd::Setup -command;
sub execute {
    my ( $self, $opt, $args ) = @_;
    say "Hello, $args->[0]";
    return 0;
};
1;
```
```
$ perl -I. say.pl greet Chicago.PM
Hello, Chicago.PM
```
---
class: middle
.center[
# Options
]
```perl
package App::Say::Command::greet;
use feature qw( say );
use App::Cmd::Setup -command;
sub opt_spec {
    return (
        [ 'greeting:s' => 'A greeting. Defaults to "Hello"' ],
    )
}
sub execute {
    my ( $self, $opt, $args ) = @_;
    $opt->{greeting} ||= "Hello";
    say "$opt->{greeting}, $args->[0]";
    return 0;
};
1;
```
```
$ perl -I. say.pl greet --greeting Bonjour Chicago.PM
Bonjour, Chicago.PM
```
---

template: title

# What Next?

---
class: center middle

# Read The Fine Manual

&bull; Getopt::Long

&bull; Pod::Usage

&bull; MooseX::Runnable

&bull; MooseX::Getopt

&bull; Capture::Tiny

&bull; App::Cmd

---
class: center middle

# .code[system() exec() qx()]
## .code[$?] in .code[perldoc perlvar]

---
template: title

Thank You
    </textarea>
    <div id="slideshow"></div>
  </body>
</html>
